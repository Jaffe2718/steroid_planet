plugins {
    id 'com.modrinth.minotaur'
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: rootProject.mod_version,
               neoforge_mc_versions: rootProject.neoforge_mc_versions,
               architectury_api_min: rootProject.architectury_api_min
    }
}

sourcesJar {
    dependsOn project(":common").sourcesJar
    from project(":common").sourcesJar.archiveFile.map { zipTree(it) }
    exclude '**/steroid_planet.mixins.json'
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    archiveVersion = "${rootProject.mod_version}+${rootProject.minecraft_version}"
    inputFile.set shadowJar.archiveFile
    from(rootDir.toPath().toString() + "/LICENSE") {
        rename { "${it}_${rootProject.archives_name}"}
    }
    exclude '**/steroid_planet.mixins.json'
    doLast {
        def publishDir = layout.buildDirectory.dir("publish").get().asFile
        publishDir.mkdirs()
        copy {
            from(remapJar.archiveFile)
            into(publishDir)
        }
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = 'yDTdQRn5'
    versionNumber = rootProject.mod_version + "-neoforge"
    versionType = rootProject.getPublishVersionType()
    uploadFile = remapJar
    loaders = ["neoforge"]
    dependencies {
        required.project 'architectury-api'
    }
    changelog = rootProject.getPublishChangelog()
}