plugins {
    id 'com.modrinth.minotaur'
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"
    modRuntimeOnly "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: rootProject.mod_version,
               fabric_loader_min: rootProject.fabric_loader_min,
               fabric_mc_versions: rootProject.fabric_mc_versions,
               architectury_api_min: rootProject.architectury_api_min

    }
}

sourcesJar {
    dependsOn project(":common").sourcesJar
    from project(":common").sourcesJar.archiveFile.map { zipTree(it) }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    archiveVersion = "${rootProject.mod_version}+${rootProject.minecraft_version}"
    inputFile.set shadowJar.archiveFile
    from(rootDir.toPath().toString() + "/LICENSE") {
        rename { "${it}_${rootProject.archives_name}"}
    }
    doLast {
        def publishDir = layout.buildDirectory.dir("publish").get().asFile
        publishDir.mkdirs()
        copy {
            from(remapJar.archiveFile)
            into(publishDir)
        }
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = 'yDTdQRn5'
    versionNumber = rootProject.mod_version + "-fabric"
    versionType = rootProject.getPublishVersionType()
    uploadFile = remapJar
    loaders = ["fabric"]
    dependencies {
        required.project 'architectury-api'
    }
    changelog = rootProject.getPublishChangelog()
}